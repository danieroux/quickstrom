import control;
import list;
import ltl;
import quickstrom/keys;

/// Returns a list of click actions on the given elements.
action click!(elements) = for element in elements. { id: "click", event: false, args: [element.ref] } when element.enabled && element.visible && element.interactable;

/// Returns a list of double-click actions on the given elements.
action doubleClick!(elements) = for element in elements. { id: "doubleClick", event: false, args: [element.ref] } when element.enabled && element.visible && element.interactable;

/// Returns a list of select actions on the given option elements.
action select!(elements) = for element in elements. { id: "select", event: false, args: [element.ref, element.value] } when element.enabled && element.visible && element.interactable;

/// Returns a list of clear actions on the given input elements.
action clear!(elements) = for element in elements. { id: "clear", event: false, args: [element.ref] } when (not element.active) && element.interactable;

/// Returns a list of focus actions on the given input elements.
action focus!(elements) = for element in elements. { id: "focus", event: false, args: [element.ref] } when (not element.active) && element.interactable;

/// An action that presses the given key. Keys are defined by Quickstrom in the `keys` object, e.g. `keys.escape`.
action keyPress!(key) = { id: "keyPress", event: false, args: [key] };

/// An action that enters the given text into the currently focused input element.
action enterText!(text) = { id: "enterText", event: false, args: [text] };

/// Returns a list of actions which enter the given text into the given input elements.
action enterTextInto!(text, elements) = for element in elements. { id: "enterTextInto", event: false, args: [text, element.ref] };

/// Emitted when the given element is visible.
action changed?(elements) = for element in elements. { id: "changed", event: true, args: [elements.ref]};

/// Emitted when the document has loaded, i.e. the DOM `load` event.
action loaded? = { id: "loaded", event: true, args: [] };

/// An action that does nothing. Use together with `timeout` to wait for a certain amount of time, or for an event to occur within that timespan.
action noop! = {id:"noop",event:false,args:[]};

/// Scroll by absolute number of pixels from the current position. Use negative `x` or `y` arguments to scroll left or up.
action scrollBy!(x, y) =
  { id: "scrollBy", event: false, args: [x, y] } when (
    // Check that the scroll position will be changed in at least one direction.
    (y != 0 &&
        if y < 0 {
          `html`.scrollTop > 0
        } else {
          `html`.scrollTop < (`html`.scrollHeight - `html`.clientHeight)
        }
    ) ||
    (x != 0 &&
      if x < 0 {
        `html`.scrollLeft > 0
      } else {
        `html`.scrollLeft < (`html`.scrollWidth - `html`.clientWidth)
      })
  );

/// Scroll by ratio of the viewport size, i.e. `scrollByViewport!(0, 1)` to scroll down one viewport height. Use negative `x` or `y` arguments to scroll left or up.
action scrollByViewport!(x, y) = scrollBy!(`html`.clientWidth * x, `html`.clientHeight * y);